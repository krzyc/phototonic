pkgname=phototonic
pkgver=1.5.48
pkgrel=1
pkgdesc="Image Viewer and Organizer"
arch=('i686' 'x86_64')
url="http://oferkv.github.io/phototonic/"
license=('GPL3')
depends=('qt5-base' 'exiv2' 'libxkbcommon-x11')
optdepends=('qt5-imageformats: TIFF and TGA support' 'qt5-svg: SVG support')
source=("https://github.com/oferkv/phototonic/archive/v${pkgver}.tar.gz")
md5sums=('d1a20189fba8ed1abda5c227d06cfe23')
provides=('phototonic')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  qmake-qt5 PREFIX="/usr" QMAKE_CFLAGS_RELEASE="$CPPFLAGS $CFLAGS" QMAKE_CXXFLAGS_RELEASE="$CPPFLAGS $CXXFLAGS" QMAKE_LFLAGS_RELEASE="$LDFLAGS"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
}

